# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=6.3.0
pkgrel=4
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libcanberra
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
options=(debug)
_tag=7fb2bef718dc29591f419a66a2027cc14626ad4c
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd gala
  git cherry-pick -n cc83db8fe398feae9f3e4caa8352b65f0c8c96d4

  # mutter 42
  git cherry-pick -n \
    7ce741413283e51509878df64149451b54dafbbb \
    cdcb87afcb4fb5729548a934885ba46b40a601ac \
    6dec2707cd3aedf8fccbb34e7b6bd261056b57ea
}

pkgver() {
  cd gala
  git describe --tags
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
