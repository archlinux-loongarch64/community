# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.12.1
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('x86_64')
url="https://github.com/dandavison/delta"
license=('MIT')
depends=('libgit2' 'libgit2.so')
makedepends=('rust' 'mdbook')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('70796f2008fbf085c6caae67165f7273a3026269851609d1b9e613aa04017715fb73bc0b5116c501acae623a32d348948f3d3afc1c99b343b623347526667471')
b2sums=('5f5a907ba6ca00e4f1382ac032d2adb9409f7f7f7666c7dbcc2bb5407ae9cf9c5f0d0b678d28aba0db2cea538fb67e45147aefff11ac07d38c55d16bf49a76ca')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
