# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=stellarsolver
pkgver=2.2
pkgrel=1
pkgdesc='The cross platform Sextractor and Astrometry.net-Based internal astrometric solver'
arch=(x86_64)
url='https://github.com/rlancaste/stellarsolver'
license=(GPL3)
depends=(qt5-base gsl wcslib hicolor-icon-theme)
makedepends=(cmake)
source=(https://github.com/rlancaste/stellarsolver/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c3a1e9456ff31b3803d13538dbf1c87be11c2c9d98010354ac65f144a9eb647f')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
