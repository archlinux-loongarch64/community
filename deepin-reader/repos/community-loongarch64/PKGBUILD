# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.10.9
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libchardet' 'libspectre' 'poppler-qt5'
         'qt5-webengine')
makedepends=('cmake' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-reader/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8b10eaa5113b142753b49c4649c87b1be3afcc8291202e45c86be651243ab0511e347dfc4532590c8e243a674caa067e9f1c432efb5c32079bd9323244982f73')

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
