# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.5.24
pkgrel=1
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=('any')
url=https://torsion.org/borgmatic
license=('GPL3')
depends=('borg' 'python-colorama' 'python-jsonschema' 'python-requests'
         'python-ruamel-yaml' 'python-setuptools')
checkdepends=('python-flexmock' 'python-pytest')
optdepends=('python-llfuse: for mount action')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('aef637e7e9a71942f289739c64bcbcd2a816cdb95a7bfc5f92f6495ce51c40e3')
b2sums=('d8c695c8faf713cd75e994d8ae7633674d01981047ce79d35d392b9db5a2dfb47c2be4a91e0f183ab2ead5877ee68983d0fdfd88ae7d28355d8029d39cbfdc04')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,root/.local,usr,' sample/systemd/$pkgname.service
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -c /dev/null \
    --ignore=tests/end-to-end
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system sample/systemd/*
}

# vim: ts=2 sw=2 et:
