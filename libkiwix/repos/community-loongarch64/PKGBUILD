# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=libkiwix
pkgver=10.1.0
pkgrel=2
pkgdesc="Library providing the Kiwix software core"
license=('GPL3')
arch=('x86_64')
provides=('kiwix-lib')
conflicts=('kiwix-lib')
replaces=('kiwix-lib')
depends=('libzim>=7.2.0' 'pugixml' 'mustache' 'xapian-core' 'aria2' 'libmicrohttpd' 'python' 'curl')
makedepends=('meson' 'gtest')
url='https://github.com/kiwix/libkiwix'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('dad49d1b6cc7bc954dccb9b108670aae647dc939cec392b77ff963f87bd47524')

build() {
	cd "${pkgname}-${pkgver}"
	# We want to ignore xapian ABI version mismatches
	arch-meson build -Dwerror=false
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}/build"
	ninja meson-test
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
