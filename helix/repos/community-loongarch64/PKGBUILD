# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=22.03
pkgrel=1
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL2')
depends=('bash')
makedepends=('cargo' 'git')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/helix-editor/helix/archive/$pkgver.tar.gz"
        "$pkgname.sh")
sha256sums=('a21f4d7b6390930a89b59567909ad7c613a1eeeafc813167ff7bc3be603997f4'
            '90d14e5c8fb65e64b508fcf4358cb45191b39e24bd6c5981814139a2a627fa5c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  local runtime_dir="$pkgdir/var/lib/$pkgname/runtime"
  mkdir -p "$runtime_dir/grammars"
  cp -r "runtime/queries" "$runtime_dir"
  cp -r "runtime/themes" "$runtime_dir"
  find "runtime/grammars" -type f -name '*.so' -exec \
    install -Dm 755 {} -t "$runtime_dir/grammars" \;
  install -Dm 644 runtime/tutor.txt -t "$runtime_dir"
  ln -s "/var/lib/$pkgname/runtime" "$pkgdir/usr/lib/$pkgname/runtime"
}

# vim: ts=2 sw=2 et:
