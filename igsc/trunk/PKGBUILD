# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=igsc
pkgver=0.5.0
pkgrel=1
pkgdesc="Intel graphics system controller firmware update library"
arch=(x86_64)
url='https://github.com/intel/igsc/'
license=(Apache)
depends=(intel-metee systemd-libs)
makedepends=(cmake systemd)
source=(https://github.com/intel/igsc/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0a4b62bdc22e2d119f2df32311dfd0f8085547a8a704ac4e2756b4535fec775e')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DENABLE_WERROR:BOOL='OFF' \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
}
