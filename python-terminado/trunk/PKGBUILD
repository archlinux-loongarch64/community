# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Will Shanks <wsha dot code at g mail dot com>

pkgname=python-terminado
_pkgname=terminado
pkgver=0.13.3
pkgrel=1
pkgdesc="Terminals served to term.js using Tornado websockets"
url="https://github.com/takluyver/terminado"
arch=('any')
license=('BSD')
depends=('python-tornado' 'python-ptyprocess')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/t/terminado/terminado-$pkgver.tar.gz")
md5sums=('2847ae71834cf485432e58d40973920e')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver

# Workaround for readline 8.1
  echo "set enable-bracketed-paste off" > .inputrc
  INPUTRC=$PWD/.inputrc \
  python setup.py test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 -m installer --destdir="$pkgdir/" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-$_pkgname/LICENSE"
}
