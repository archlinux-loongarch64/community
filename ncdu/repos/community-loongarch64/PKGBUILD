# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lp76 <l.peduto@gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname='ncdu'
pkgver=2.1.1
pkgrel=1
pkgdesc='Disk usage analyzer with an ncurses interface'
url='https://dev.yorhel.nl/ncdu'
license=('MIT')
depends=('ncurses')
makedepends=('zig')
arch=('x86_64')
source=("https://dev.yorhel.nl/download/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "hardening.patch")
sha256sums=('d6c2374ca50d40b6211346972a2e89c9601172fd0704d885eda5b1e09a2a48ed'
            'SKIP'
            '4b90e881d1dd5fb36b269e52952be40e050926dd6d4ce08ac7a7f0e5940e790d')
b2sums=('27857c110b80d2b90313d628dd0412dd281948ccae924c9b2b3e870232786f02006658899668e1a342b7f845ef992358cfc0cc06899f3008cb34baddca34951e'
        'SKIP'
        '54b47c408e4144bee629d1d80410e4c97baf375d10399da3644a7a929b21732b8323723b2218890fe7d7d5b9b7b42427a8af74bc64c4002ef23e722c3ed969bf')
validpgpkeys=('74460D32B80810EBA9AFA2E962394C698C2739FA') # Yoran Heling <projects@yorhel.nl>

prepare() {
    cd "${pkgname}-${pkgver}"

    patch --verbose --strip=1 --input=../hardening.patch
}

build() {
    cd "${pkgname}-${pkgver}"

    # since RELRO is not doable in the zig.build file (https://github.com/ziglang/zig/issues/6977)
    # we are running zig build-exe directly
    #zig build -Drelease-safe -Dcpu=baseline

    zig build-exe ./src/main.zig ./src/ncurses_refs.c -lc -lncursesw -OReleaseSafe --name ncdu -target native-native -mcpu x86_64 -D _DEFAULT_SOURCE -D _XOPEN_SOURCE=600 -fPIE -z relro -z now -fno-each-lib-rpath
}

check() {
    cd "${pkgname}-${pkgver}"

    zig build test
}

package() {
    cd "${pkgname}-${pkgver}"

    install --verbose -D --mode=755 "./${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    make install-doc PREFIX="${pkgdir}/usr"

    install --verbose -D --mode=644 LICENSES/MIT.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
