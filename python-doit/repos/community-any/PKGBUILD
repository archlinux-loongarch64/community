# Maintainer: David Runge <dvzrv@archlinux.org>

_name=doit
pkgname=python-doit
pkgver=0.35.0
pkgrel=1
pkgdesc="A task management and automation tool"
arch=(any)
url="https://pydoit.org/"
license=(MIT)
depends=(python-cloudpickle python-pyinotify)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-doit-py python-pyflakes python-pytest python-toml)
optdepends=(
  'python-toml: for TOML support'
  'python-setuptools: for plugin support'
  'bash-completion: autocomplete for bash'
  'zsh-completions: autocomplete for ZSH'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('3eb3fc0284c4779db47c56c9e21bda16e31d053214f4a0272c1a26c9fd26bc0de7d00601bb08a79a3d0ed403464e773c03fe93d0fcd9ee59812c396e0f4ed7e6')
b2sums=('db0c1c9245897ca9c4ce4d3f5f43e5f2e5035c36cc4bd15e63d6fb9e69992b5068ca24839a02485680cdc75ba62cb95f139f6fd0ec24c1f26fd4c4b2e4fce27e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 bash_completion_$_name "$pkgdir/usr/share/bash-completion/completions/$_name"
  install -vDm 644 zsh_completion_$_name "$pkgdir/usr/share/zsh/site-functions/_$_name"
}
