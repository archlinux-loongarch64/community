# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=22.2
pkgrel=1
pkgdesc="An advanced sound editor"
arch=(x86_64)
url="https://ccrma.stanford.edu/software/snd/"
license=(custom:free)
groups=(pro-audio)
depends=(glibc glu gmp gsl libglvnd libmpc libx11 libxext
libxpm libxt mpfr openmotif)
# TODO: maybe replace mpg123 with mpg321
makedepends=(alsa-lib fftw flac gendesk jack ladspa libpulse
libsamplerate mpg123 ruby speex timidity++ vorbis-tools wavpack)
optdepends=(
  'flac: For flac support'
  'mpg123: For MPEG support'
  'pulseaudio: For pulseaudio support'
  'ruby: for snd-ruby'
  'speex: For speex support'
  'timidity++: For MIDI to WAVE support'
  'vorbis-tools: For OGG support'
  'wavpack: For wav support'
)
options=(debug)
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3c4550a0cb8b6ba92a95a1cdf94c6ede194ef110fe94c36e947a58916156ae70cd8f0112724b1655b082c934392bbc8eab363e3c3fc9cc465bd625bd6e7fe08a')
b2sums=('455bf2ba4f9168773cf21fbe872694d0fb7598b03f982aa9caf487981dab5cb651ee622af8771b420e6b8f78ee4de7e4e5bd1379764857629a2a442de45c444d')

prepare() {
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-s7" \
          --name "$pkgname (s7)" \
          --exec "$pkgname-s7" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-ruby" \
          --name "$pkgname (ruby)" \
          --exec "$pkgname-ruby" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname" \
          --name $pkgname \
          --exec $pkgname \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  rm -v $pkgname-$pkgver/*.desktop
  cp -av $pkgname-$pkgver $pkgname-ruby-$pkgver
}

build() {
  (
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-s7
  make
  )
  (
  cd $pkgname-ruby-$pkgver
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-ruby
  make
  )
}

package() {
  depends+=(libasound.so libfftw3.so libjack.so libsamplerate.so)

  (
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" install
  mv -v "$pkgdir/usr/bin/snd"{,-s7}
  )

  cd "$pkgname-ruby-$pkgver"
  make prefix="$pkgdir/usr" install
  mv -v "$pkgdir/usr/bin/snd"{,-ruby}

  # set s7 version as default
  ln -sv snd-s7 "$pkgdir/usr/bin/snd"

  # XDG desktop integration
  install -vDm 644 ../*.desktop -t "$pkgdir/usr/share/applications/"
  # license
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 *.html -t "$pkgdir/usr/share/doc/$pkgname/html"
  install -vDm 644 pix/*.png -t "$pkgdir/usr/share/doc/$pkgname/html/pix"

  # remove examples of unsupported extension language forth (not in the repos)
  rm -v "$pkgdir/usr/share/$pkgname/"*.fs
}
