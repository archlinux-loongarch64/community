# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

pkgname=unpaper
pkgver=6.1
pkgrel=9
pkgdesc="post-processing tool for scanned sheets of paper"
arch=('x86_64')
url="https://github.com/Flameeyes/unpaper"
license=("GPL")
depends=('ffmpeg')
makedepends=('libxslt' 'docbook-xsl')
source=("$pkgname-${pkgver}.tar.gz::https://github.com/Flameeyes/unpaper/archive/unpaper-$pkgver.tar.gz"
         ffmpeg5.patch)
sha256sums=('213f8143b3361dde3286537eb66aaf7cdd7e4f5e7bde42ac6e91020997a81f1d'
            'f0d2d96ce399acb41ab8392855f857fcd0e3b1e5d869bb5753a90633c4dfb977')

prepare() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  aclocal
  automake --add-missing
  autoconf

# Fix build with FFmpeg 5
  patch -p1 -i ../ffmpeg5.patch
}

build() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/unpaper-unpaper-$pkgver
  make install DESTDIR="$pkgdir"
}
