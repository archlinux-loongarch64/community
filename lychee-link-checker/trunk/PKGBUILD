# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=lychee
pkgname=lychee-link-checker
pkgver=0.9.0
pkgrel=1
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
replaces=('lychee-rs')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('3d0158aa14db4d7947fd122656d2e3a6c9dd126213f82590dea79e6493aacb44d7f74b4f2a3fbe4621558e336516aca5ec860dcebf3a9971aa9e4a29c28fdda8')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
