# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2.27.0
pkgver=2.27.0
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
#_patchver=2.25.0 # almost always out of sync now
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-gallium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        "$pkgname-v$_pkgver-$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'bb0ef449e7846e57b68abb9dee1f5652f705dee1c1ace887757150a28f5fa27ef147a53e38bbec553147180a942e6572a83c75907e11f1a44a97af7076e555be')

pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-web
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_pkgver-$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-web
  npm run dist:oss:selfhost
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
