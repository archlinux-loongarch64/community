# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-unikey
pkgver=5.0.9
pkgrel=1
pkgdesc="Unikey engine support for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-unikey"
license=('GPL')
depends=('fcitx5-qt')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('97fba88e1a2629ed3ca406b5ae9dbe6108790e5fcc03ed2193e95de6421a8fadc9b6db743b332e5366855ef9540f4536071904347fd9b4fcfc8b60dbad151abd'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
