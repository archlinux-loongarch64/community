# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-readme-renderer
pkgver=34.0
pkgrel=1
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('any')
depends=('python-bleach' 'python-docutils' 'python-pygments' 'python-six')
optdepends=('python-cmarkgfm: for Markdown support')
makedepends=('python-setuptools' 'python-cmarkgfm')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('cb79d1fd362a2ddafb6b60bc630ce7d9234bacc26ab04a4bbfae7fd7c183fc48be1f5940834a0133c65c89c5ba8074e1ae46eee4a5d6cc35cfc12d1a3b329601')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package_python-readme-renderer() {
  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
