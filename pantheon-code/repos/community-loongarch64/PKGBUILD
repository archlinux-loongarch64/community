# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=6.1.0
pkgrel=3
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL3)
groups=(pantheon)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview4
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libpeas
  pango
  polkit
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vala
  vte3
  webkit2gtk
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
  'webkit2gtk: Browser Preview plugin'
)
_tag=4a111ecbdc3060b36ef6ffb23510a0f0b52129be
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-code
  git cherry-pick -n 4f5bb5118cefb3f37dda8a78a141cde5d81b1227
}

pkgver() {
  cd pantheon-code
  git describe --tags
}

build() {
  arch-meson pantheon-code build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
