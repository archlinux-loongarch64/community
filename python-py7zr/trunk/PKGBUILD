# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-py7zr
_pyname=${pkgname#python-}
pkgver=0.18.3
pkgrel=1
pkgdesc='Pure python 7-zip library'
arch=(any)
url="https://github.com/miurahr/${_pyname}"
license=(LGPL)
depends=(python-pycryptodomex
         python-texttable
         python-multivolumefile
         python-pyppmd
         python-pyzstd
         python-brotlicffi
         python-zipfile-deflate64)
optdepends=('python-zstandard: zstd compression')
makedepends=(python-{build,installer}
             python-setuptools-scm
             python-wheel)
checkdepends=(python-pytest-benchmark
              python-pytest-timeout
              pyannotate
              p7zip
              python-py-cpuinfo)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('d5bdc81536316f209a3ca4e1ce6e2ef11d8a519d613be47bcdd084bf655f400f')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m pytest -rsx -m 'not benchmark and not remote_data'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
