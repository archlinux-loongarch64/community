# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=2.3.0
pkgrel=2
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
makedepends=(python python-setuptools)
depends=(
    ipython
    python-cloudpickle
    python-ipykernel
    python-jupyter_client
    python-pyzmq
    python-wurlitzer
)
checkdepends=(
    cython
    python-dask
    python-distributed
    python-flaky
    python-matplotlib
    python-mock
    python-numpy
    python-pandas
    python-pillow
    python-pytest
    python-scipy
    python-xarray
)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3fbf18088fdd3c4d3ca1923790b2537d08f52bb7e616a15a544534ef8e22d05f')

prepare() {
  cd ${_pkg}-${pkgver}
  sed -i 's|ipython>=7.31.1,<8|ipython>=7.31.1|' setup.py
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -vv --color=yes spyder_kernels
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
