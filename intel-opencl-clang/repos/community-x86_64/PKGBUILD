# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=13.0.0.r5+g1490bd4
pkgrel=1
pkgdesc="Wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang"
license=(custom)
depends=(clang llvm-libs spirv-llvm-translator)
makedepends=(cmake llvm git)
# Adapted from IGC release notes bbdd1587f577397a105c900be114b56755d1f7dc
_commit=1490bd4bcbce0cc91aa9e93c63988bf7e09bc2db
source=(git+${url}.git#commit=$_commit)
sha256sums=(SKIP)

pkgver() {
  cd ${_srcname}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./;s/-/+/'
}

build() {
  cmake -B build -S ${_srcname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
    -DSPIRV_TRANSLATOR_DIR=/usr \
    -DLLVM_NO_DEAD_STRIP=ON \
    -DPREFERRED_LLVM_VERSION=13.0.1 \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_srcname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
