# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyterlab_pygments
pkgver=0.2.0
pkgrel=1
pkgdesc="Pygments theme using JupyterLab CSS variables"
arch=('any')
#url="https://pypi.org/project/jupyterlab-pygments/"
url="https://github.com/jupyterlab/jupyterlab_pygments"
license=('BSD')
depends=('python-pygments' 'jupyterlab')
# NPM package @jupyterlab/codemirror ?
makedepends=('python-build' 'python-installer')
source=(https://pypi.io/packages/source/j/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('2d48bcdd666043afc086af56adaf6bb79bbeffb1d73ed00ec4a2113f6cc22581')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
