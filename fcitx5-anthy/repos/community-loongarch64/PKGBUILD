# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=5.0.10
pkgrel=1
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL')
depends=('fcitx5' 'anthy')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-anthy/fcitx5-anthy-$pkgver.tar.xz"{,.sig})
sha512sums=('339bd09bdc1b88f4f1a61685bd3a80d28cdc755c27227b7db07f90c0c318b83a7fb4a538e39b1969bf8f480809ddce64b16b9bcb1f08e4a1b07d6dde9f1ac3a4'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
