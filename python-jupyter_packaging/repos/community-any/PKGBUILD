# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter_packaging
pkgname=python-jupyter_packaging
pkgver=0.12.0
pkgrel=3
pkgdesc='Jupyter Packaging Utilities'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-packaging python-deprecation python-wheel python-tomlkit)
makedepends=(python-build python-installer python-setuptools)
checkdepends=(python-pytest-mock)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz
        https://github.com/jupyter/jupyter-packaging/commit/a3b4c9b9.patch)
sha256sums=('b27455d60adc93a7baa2e0b8f386be81b932bb4e3c0116046df9ed230cd3faac'
            '281f1b6a727835673d7f812991f98a2cb399fae9b79730ac428724ef09fceb75')

prepare() {
  patch -d $_pipname-$pkgver -p1 < a3b4c9b9.patch # Don't install tests in site-packages
  sed -e 's|setuptools.*|setuptools|' -i $_pipname-$pkgver/setup.cfg # Remove version constraint
}

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -vv
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
