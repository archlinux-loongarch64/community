# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=2.2.0
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rust' 'cmake' 'llvm11')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/${pkgver}.tar.gz")
sha512sums=('4e7e0bd7b209414a5fcb4cf5c479d9b57d78cc25e406c22e63702ad8f9bb9813ff471556fe2b4c65fc6f9d6ec9e725589091c71c9dcd37257627cdebd1de2c62')
options=('staticlibs' '!lto')

WASMER_INSTALL_PREFIX=/usr
ENABLE_LLVM=1

build() {
  cd "$pkgname-$pkgver"
  make all
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/usr make install
}

# vim:set ts=2 sw=2 et:
