# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-regexp
pkgver=0.084
pkgrel=1
pkgdesc="Parse regular expressions"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Regexp"
depends=('perl-list-moreutils' 'perl-ppi' 'perl-task-weaken')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz")
sha512sums=('76200198a9e72831ba6a1e68c55dc2121c750760232ce9402f3a851efe3cf7ee2533630ef18158cebce291aad97202cf5e23583adb91800b175be9dadd397855')

build() {
  cd PPIx-Regexp-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Regexp-$pkgver
  make test
}

package() {
  cd PPIx-Regexp-$pkgver
  make DESTDIR="$pkgdir" install
}
